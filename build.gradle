plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'com.github.gmazzo.buildconfig' version '5.3.5'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

buildConfig {
    buildConfigField(String, "VERSION", project.mod_version)
    buildConfigField(String, "BUILD_IDENTIFIER", project.build_identifier)
    buildConfigField(String, "BUILD_TIME", "${new Date(System.currentTimeMillis()).format("MM/dd/yyyy HH:mm")}")

    packageName "dev.sakura"
    useJavaOutput()
    generateAtSync = true
    documentation.set("Generated by BuildConfig plugin")
}

loom {
    accessWidenerPath = file("src/main/resources/sakura.accesswidener")
}

sourceSets.main.java.srcDirs += "build/gen/buildconfig/src/main"

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://impactdevelopment.github.io/maven/" }
    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://raw.githubusercontent.com/cabaletta/baritone/master/maven/" }
    maven { url "https://maven.isxander.dev/" }
    maven {
        name = "meteor-maven"
        url = "https://maven.meteordev.org/releases"
    }
    maven {
        name = "meteor-maven-snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "com.github.cabaletta:baritone:${baritone_api_version}"
    implementation include("meteordevelopment:orbit:${project.orbit_version}")

    implementation include("org.bytedeco:javacv-platform:1.5.10")

    // NanoVG渲染库
    implementation include (group: 'org.lwjgl', name: 'lwjgl-nanovg', version: '3.3.3')
    implementation include ("org.lwjgl:lwjgl-nanovg::natives-windows")
    
    // ByteBuddy依赖用于抓取Mixin
    implementation include("net.bytebuddy:byte-buddy:1.14.18")
    implementation include("net.bytebuddy:byte-buddy-agent:1.14.18")

    implementation shadow(fileTree(dir: 'libs', includes: ['annotations.jar']))
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
    it.options.encoding = 'UTF-8'

    dependsOn generateBuildConfig
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set("dev")
}

remapJar {
    dependsOn(shadowJar)
    inputFile = tasks.shadowJar.archiveFile
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.register('buildClientOnly') {
    doFirst {
        tasks.remapLoader.enabled = false
    }
    finalizedBy 'build'
}
