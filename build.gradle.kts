import java.text.SimpleDateFormat
import java.util.Date
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.fabricmc.loom.task.RemapJarTask
import net.fabricmc.loom.api.LoomGradleExtensionAPI

plugins {
    id("fabric-loom") version "1.9-SNAPSHOT"
    id("com.github.gmazzo.buildconfig") version "5.3.5"
    id("com.gradleup.shadow") version "9.0.0-beta4"
    java
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

base {
    archivesName.set(property("archives_base_name").toString())
}

version = property("mod_version")!!
group = property("maven_group")!!

buildConfig {
    buildConfigField("String", "VERSION", "\"${property("mod_version")}\"")
    buildConfigField("String", "BUILD_IDENTIFIER", "\"${property("build_identifier")}\"")
    buildConfigField("String", "BUILD_TIME", "\"${SimpleDateFormat("MM/dd/yyyy HH:mm").format(Date())}\"")

    packageName("dev.sakura.client")
    useJavaOutput()
    generateAtSync.set(true)
    documentation.set("Generated by BuildConfig plugin")
}

configure<LoomGradleExtensionAPI> {
    accessWidenerPath.set(file("src/main/resources/sakura.accesswidener"))
}

sourceSets["main"].java.srcDirs("build/gen/buildconfig/src/main")

repositories {
    maven {
        name = "AliyunPublic"
        url = uri("https://maven.aliyun.com/repository/public")
    }
    maven {
        name = "AliyunGoogle"
        url = uri("https://maven.aliyun.com/repository/google")
    }
    mavenCentral()
    maven("https://jitpack.io")
    maven("https://impactdevelopment.github.io/maven/")
    maven("https://maven.fabricmc.net/")
    maven("https://raw.githubusercontent.com/cabaletta/baritone/master/maven/")
    maven("https://maven.isxander.dev/")
    maven("https://maven.meteordev.org/releases")
    maven("https://maven.meteordev.org/snapshots")
}

dependencies {
    "minecraft"("com.mojang:minecraft:${property("minecraft_version")}")
    "mappings"("net.fabricmc:yarn:${property("yarn_version")}:v2")

    "modImplementation"("net.fabricmc:fabric-loader:${property("loader_version")}")
    "modImplementation"("net.fabricmc.fabric-api:fabric-api:${property("fabric_version")}")
    "modImplementation"("com.github.cabaletta:baritone:${property("baritone_api_version")}")
    
    implementation(include("meteordevelopment:orbit:${property("orbit_version")}")!!)
    //TODO MAC OS SUPPORT
    //定义 LWJGL Native 平台检测
    val lwjglNatives = run {
        val os = System.getProperty("os.name").lowercase()
        val arch = System.getProperty("os.arch").lowercase()
        when {
            os.contains("win") -> "natives-windows"
            os.contains("mac") || os.contains("darwin") -> {
                if (arch.contains("aarch64") || arch.contains("arm64")) "natives-macos-arm64"
                else "natives-macos"
            }
            os.contains("linux") -> {
                if (arch.contains("aarch64") || arch.contains("arm")) "natives-linux-arm64"
                else "natives-linux"
            }
            else -> "natives-windows"
        }
    }

    // NanoVG 运行库
    val lwjglVersion = "3.3.3"
    implementation(include("org.lwjgl:lwjgl-nanovg:$lwjglVersion")!!)

    // 关键修复：根据当前系统动态引入 natives
    // runtimeOnly 保证运行时可用，include 保证打包进 Jar
    runtimeOnly(include("org.lwjgl:lwjgl-nanovg:$lwjglVersion:$lwjglNatives")!!)

    // 建议同时包含 lwjgl 核心的 natives 以防万一
    runtimeOnly(include("org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives")!!)

    // Java Agent
    implementation(include("net.bytebuddy:byte-buddy:1.14.18")!!)
    implementation(include("net.bytebuddy:byte-buddy-agent:1.14.18")!!)
}

tasks.processResources {
    inputs.property("version", project.version)
    filesMatching("fabric.mod.json") {
        expand(project.properties)
    }
}

tasks.withType<JavaCompile>().configureEach {
    options.release.set(21)
    options.encoding = "UTF-8"
    dependsOn("generateBuildConfig")
}

tasks.named<ShadowJar>("shadowJar") {
    configurations = listOf(project.configurations["shadow"])
    archiveClassifier.set("dev")
}

tasks.named<RemapJarTask>("remapJar") {
    dependsOn("shadowJar")
    inputFile.set(tasks.named<ShadowJar>("shadowJar").get().archiveFile)
}

/*tasks.register<Jar>("buildCloudPayload") {
    dependsOn("remapJar")
    from(zipTree(tasks.named<RemapJarTask>("remapJar").get().archiveFile)) {
        exclude("fabric.mod.json")
    }

    doFirst {
        val mixinJsonFile = project.file("src/main/resources/sakura.mixins.json")
        if (mixinJsonFile.exists()) {
            val content = mixinJsonFile.readText()
            val packageMatch = Regex("\"package\"\\s*:\\s*\"([^\"]+)\"").find(content)
            val pkg = packageMatch?.groupValues?.get(1) ?: ""

            val clientMatch = Regex("\"client\"\\s*:\\s*\\[(.*?)\\]", RegexOption.DOT_MATCHES_ALL).find(content)
            val clientBlock = clientMatch?.groupValues?.get(1) ?: ""
            
            val mixins = Regex("\"([^\"]+)\"").findAll(clientBlock).map { it.groupValues[1] }.toList()
            
            val listFile = File(temporaryDir, "mixins.list")
            listFile.parentFile.mkdirs()
            listFile.writeText(mixins.joinToString("\n") { 
                if (pkg.isNotEmpty()) "$pkg.$it" else it 
            })
            
            from(listFile)
        }
    }
    
    archiveClassifier.set("payload")
}*/

tasks.jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.register("buildClientOnly") {
    doFirst {
         tasks.findByName("remapLoader")?.enabled = false
    }
    finalizedBy("build")
}

/*tasks.register<Copy>("copyLibs") {
    from(configurations.runtimeClasspath)
    from(configurations.compileClasspath)
    into(layout.projectDirectory.dir("混淆/ZKM/libs"))
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}*/
